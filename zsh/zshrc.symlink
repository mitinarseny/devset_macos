#!/bin/zsh

export DOTFILES="$(readlink --canonicalize-existing "$(dirname "$(readlink --canonicalize-existing "${HOME}/.zshrc")")/..")"

_zcompinit_custom() {
  autoload -Uz compinit
  local zcd="${HOME}/.zcompdump"
  local zcdc="${zcd}.zwc"
  # Compile the completion dump to increase startup speed, if dump is newer or doesn't exist,
  # in the background as this is doesn't affect the current session
  if [[ -f "${zcd}"(#qN.m+1) ]]; then
        compinit -i -d "${zcd}"
        { rm -f "${zcdc}" && zcompile "${zcd}" } &!
  else
        compinit -C -d "${zcd}"
        { [[ ! -f "${zcdc}" || "${zcd}" -nt "${zcdc}" ]] && rm -f "${zcdc}" && zcompile "${zcd}" } &!
  fi
}

typeset -U config_files
config_files=(${DOTFILES}/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "${file}"
done

# load antibody plugins
source ~/.zsh_plugins.sh

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "${file}"
done

# init completion
_zcompinit_custom

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "${file}"
done

unset config_files

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
[ -f ~/.localrc ] && . ~/.localrc
